
@top Program { FrontMatter? FrontMatterSplitter? (expression | Import | Binding)* }

@skip { space | BlockComment }

expression {
    Semi |
    Comma |
    Assignment |
    AngleBracketOpen |
    Number |
    StringLiteral |
    SubParser |
    Placeholder |
    VariableName  |
    Quote |
    Pragma |
    Expression { OpcodeOpening expression* ParenClose }
}

OpcodeOpening {
    Opcode | 
    OpcodeWithOperandOpening { OpcodeWithOperand expression* AngleBracketClose ParenOpen } 
}

@local tokens {
    blockCommentEnd { "*/" }
    blockCommentNewline { "\n" | "\r" | "\r\n" }
    @else blockCommentContent
}

@external tokens frontMatter from "./tokens" {
    FrontMatter
}

@skip {} {
    BlockComment { "/*" (blockCommentContent | blockCommentNewline)* blockCommentEnd }
}

@tokens {

    Quote { "'" ![^ \t\n\r()<>:,;]* }

    Binding { "#" ![^ \t\n\r()<>:,;]* }

    Import { "@" $[ \t\n\r]* ((word) space)? "0x" $[a-fA-F0-9]+ }

    Placeholder { "_" }

    word { @asciiLetter+ ( @asciiLetter | @digit | "-" )* }

    Opcode { word ParenOpen }

    OpcodeWithOperand { word AngleBracketOpen }

    Pragma { "using-words-from" }

    FrontMatterSplitter { "---" }

    VariableName { word }

    Number {
      @digit |
      "0x" $[a-fA-F0-9]+ |
      $[1-9] (@digit)* "e" (@digit)+
    }

    StringLiteral { "\"" !["] "\"" }

    SubParser { "[" ![\[\]] "]" }

    AngleBracketOpen { "<" }

    AngleBracketClose { ">" }

    ParenOpen { "(" }

    ParenClose { ")" }

    Comma { "," }

    Semi { ";" }

    Assignment { ":" }

    space { $[ \t\n\r]+ }

    @precedence { Pragma, VariableName }

    @precedence { Opcode, VariableName }

    @precedence { OpcodeWithOperand, VariableName }
   
}

@external propSource highlight from "./props"
@external propSource indent from "./props"
@external propSource fold from "./props"

@detectDelim